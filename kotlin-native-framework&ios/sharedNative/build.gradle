apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'

repositories {
    mavenCentral()
}

group 'com.avinash.weather.lib'
version '1.0.1'

apply plugin: 'maven-publish'

buildscript {
    ext.ios_framework_name = 'KotlinShared'
}

kotlin {
    targets {

        fromPreset(presets.jvm, 'android')

        final def iosTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") \
                              ? presets.iosArm64 : presets.iosX64

        fromPreset(iosTarget, 'ios') {
            binaries {
                framework("$ios_framework_name") {
                    embedBitcode('disable')
                }
            }
        }
    }
    sourceSets {
        commonMain.dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.1.1'
            implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.9.0'
            implementation 'io.ktor:ktor-client-core:1.1.3'
            implementation 'io.ktor:ktor-client-json:1.1.3'
        }

        iosMain.dependencies {
            implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.1.1'
            implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:0.9.0'
            implementation 'io.ktor:ktor-client-core-native:1.1.3'
            implementation 'io.ktor:ktor-client-ios:1.1.3'
            implementation 'io.ktor:ktor-client-json-native:1.1.3'
        }

        androidMain.dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib'
            implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
            implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.0'
            implementation 'io.ktor:ktor-client-core-native:1.1.3'
            implementation 'io.ktor:ktor-client:1.1.3'
            implementation 'io.ktor:ktor-client-json-native:1.1.3'
        }
    }
}

// Task for packaging IOS source code to a frame work to be used in Xcode
task packForXcode {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String buildType = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'

    def keyFrameworkPrefix = "$ios_framework_name${buildType.toLowerCase().capitalize()}"
    dependsOn "link${keyFrameworkPrefix}FrameworkIos"

    doLast {
        def srcFile = kotlin.targets.ios.binaries.getFramework("$ios_framework_name", buildType).outputFile

        copy {
            from srcFile.parent
            into frameworkDir
        }

        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}

tasks.build.dependsOn packForXcode
